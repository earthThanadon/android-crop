apply plugin: 'com.android.library'
apply plugin: 'signing'
apply plugin: 'maven-publish'

android {
    namespace = "com.earththanadon.android.crop"
    compileSdkVersion 35

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 35

        testApplicationId 'com.earththanadon.android.crop.test'
        testInstrumentationRunner 'android.test.InstrumentationTestRunner'
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.earththanadon'
            artifactId = 'android-crop'
            version = 'v1.0.4'

            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                project.configurations.api.allDependencies.each { dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                }
            }
        }
    }
}

tasks.withType(PublishToMavenLocal).all { publishTask ->
    if (publishTask.name.contains('release')) {
        publishTask.dependsOn(tasks.named('bundleReleaseAar'))
    }
}

dependencies {
    implementation 'com.android.support:support-annotations:23.0.1'
    implementation 'com.android.support:support-v4:23.0.1'
    testImplementation 'com.squareup:fest-android:1.0.7'
    testImplementation 'com.android.support:support-v4:23.0.1'
    testImplementation 'org.mockito:mockito-core:1.9.5'
    testImplementation 'com.google.dexmaker:dexmaker:1.0'
    testImplementation 'com.google.dexmaker:dexmaker-mockito:1.0'
}

